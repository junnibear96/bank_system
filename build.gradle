plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'com.finance'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    
    // Yahoo Finance API
    implementation 'com.yahoofinance-api:YahooFinanceAPI:3.17.0'
    
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

node {
    download = true
    version = '20.11.0'
    npmVersion = '10.2.4'
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
    nodeProjectDir = file("${project.projectDir}/finance-front")
}

task npmInstallDependencies(type: NpmTask) {
    args = ['install']
}

task npmBuild(type: NpmTask) {
    args = ['run', 'build']
    dependsOn npmInstallDependencies
}

task copyFrontend(type: Copy) {
    from "${project.projectDir}/finance-front/dist"
    into "${project.buildDir}/resources/main/static"
    dependsOn npmBuild
}

processResources {
    dependsOn copyFrontend
}
